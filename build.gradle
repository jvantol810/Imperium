plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.0'
	id 'io.spring.dependency-management' version '1.1.6'
}

group = 'Imperium'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(23)
	}
}

// Include the dependencies.gradle file
apply from: 'dependencies.gradle'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

tasks.register('runJarWithToolchain', Exec) {
	dependsOn tasks.build, tasks.jar

	def jarFile = layout.buildDirectory.file("libs/${project.name}-${project.version}.jar").get().asFile

	def javaLauncher = javaToolchains.launcherFor {
		languageVersion = JavaLanguageVersion.of(23)
	}.get()

	executable = javaLauncher.executablePath.asFile.absolutePath
	args = ['-jar', jarFile.absolutePath]
}

tasks.named('test') {
	useJUnitPlatform()
}

// Define values
def imageName = 'listmaster'
def containerName = 'listmaster'
def jarFile = "build/libs/ListMaster-${project.version}.jar"

// Task: Build Docker image
tasks.register("buildDockerImage", Exec) {
	dependsOn bootJar  // Ensures JAR is built first
	commandLine 'docker', 'build', '-t', imageName, '.'
}

// Task: Run Docker container
tasks.register("runDockerContainer", Exec) {
	dependsOn buildDockerImage
	doFirst {
		// Optional: stop and remove existing container
		exec {
			commandLine 'docker', 'rm', '-f', containerName
			ignoreExitValue = true
		}
	}
	commandLine 'docker', 'run', '--name', containerName, '-p', '3000:3000', imageName
}

// Convenience task to do all in one
tasks.register("buildAndRunDocker") {
	dependsOn runDockerContainer
	group = 'docker'
	description = 'Builds the JAR, builds the Docker image, and runs the container'
}
